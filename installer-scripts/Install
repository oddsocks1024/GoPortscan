; $VER: Go Portscan 1.1 Install (OS3.5+)
; By Ian Chapman

; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(effect "center" "radial" $FFFF00 $FF0000)

(procedure p_copynicons

    (set #temp @default-dest)
    (copyfiles
        (source "icons/gpexe.new.info")
        (newname "GoPortscan!.info")
        (dest @default-dest)
    )
    (copyfiles
        (source "icons/gpexe.new.info")
        (newname "def_goportscan.info")
        (dest "ENVARC:Sys/")
    )
    (copyfiles
        (source "icons/doc_en.new.info")
        (newname "GoPortscan!_en.guide.info")
        (dest @default-dest)
    )
    (set #temp (pathonly #temp ))
    (copyfiles
        (source "icons/gpdir.new.info")
        (newname "GoPortscan!.info")
        (dest #temp)
    )
)

(procedure p_copy35icons
    (set #temp @default-dest)

    (copyfiles
        (source "icons/gpexe.35.info")
        (newname "GoPortscan!.info")
        (dest @default-dest)
    )

    (copyfiles
        (source "icons/gpexe.35.info")
        (newname "def_goportscan.info")
        (dest "ENVARC:Sys/")
    )
    (copyfiles
        (source "icons/doc_en.35.info")
        (newname "GoPortscan!_en.guide.info")
        (dest @default-dest)
    )
    (set #temp (pathonly #temp ))
    (copyfiles
        (source "icons/gpdir.35.info")
        (newname "GoPortscan!.info")
        (dest #temp)
    )    
)


; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(showmedia '#logo' "images/logo.jpg" "upper_left" "none" 0)

(message "Welcome to the Go Portscan! 1.1 Installation Utility\n By Ian Chapman\n\nGo Portscan! is a TCP/UDP Portscanner\nwith service lookup and many other features.")

(set @default-dest
    (askdir
        (prompt "Where would you like to install Go Portscan! 1.1?\n(Note: A new directory called GoPortscan! will be created, if it does not exist.)")
        (help "The installer is asking where you would like Go Portscan! and related files to be installed. A new directory called GoPortscan! will be created for you, if it does not already exist. Currently no backups are made of an existing installation.")
        (default @default-dest)
    )
)

(set @default-dest (tackon @default-dest "GoPortscan!"))
(makedir @default-dest)

; Catalog related stuff

(set #catalogdir @default-dest)
(set #catalogdir (tackon #catalogdir "catalogs"))
(makedir #catalogdir)
(copyfiles
    (source "catalogs/GoPortscan.cd")
    (dest #catalogdir)
)

(set #contribdir @default-dest)
(set #contribdir (tackon #contribdir "contrib"))
(makedir #contribdir)
(copyfiles
    (source "contrib/keyhole-eye.info")
    (dest #contribdir)
)

(set #instlang
    (askoptions
        (default -1)
        (prompt "Which locales do you wish to install? (English is built in)")
        (help "The installer is asking which languages you wish to install. Go Portscan will display its interface in any language for which a catalog exists")
        (choices "Magyar V1.1 (Hungarian) by Marton Dosa")
    )
)

(if
    (= 1 #instlang)
    (
        (set #catalogdir (tackon #catalogdir "magyar"))
        (makedir #catalogdir)
        (copyfiles
            (source "catalogs/magyar/GoPortscan.catalog")
            (dest #catalogdir)
        )
    )
)

(copyfiles
    (source "exes/GoPortscan!.68000")
    (newname "GoPortscan!")
    (dest @default-dest)
)

(set #docdir @default-dest)

(askdir
    (prompt "Where would you like me to place the documentation?\n(Currently documentation is only in English)")
    (help "The installer is asking where you would like the Go Portscan! documentation installed. If you are unsure then choose the default location. Note: If you wish to use online help then you must install the documentation in the default location")
    (default #docdir)
)

(copyfiles
    (infos)
    (source "docs/GoPortscan!_en.guide")
    (dest #docdir)
)

(copylib
    (prompt "About to install reqtools.library")
    (source "libs/reqtools.library")
    (dest "libs:")
    (help "Reqtools is a required library for use with Go Portscan!. You must have it installed in order to use Go Portscan!")
    (confirm)
)

(copylib
    (prompt "About to install Busy.mcc")
    (source "libs/Busy.mcc")
    (dest "MUI:libs/mui")
    (help "This is a required MUI library. You must have it installed in order to use Go Portscan!")
    (confirm)
)

(copylib
    (prompt "About to install Busy.mcp")
    (source "libs/Busy.mcp")
    (dest "MUI:libs/mui")
    (help "This is a required MUI library. You must have it installed in order to use Go Portscan!")
    (confirm)
)

(copylib
    (prompt "About to install BetterString.mcc")
    (source "libs/BetterString.mcc")
    (dest "MUI:libs/mui")
    (help "This is a required MUI library. You must have it installed in order to use Go Portscan!")
    (confirm)
)

(copylib
    (prompt "About to install BetterString.mcp")
    (source "libs/BetterString.mcp")
    (dest "MUI:libs/mui")
    (help "THis is a required MUI library. You must have it installed in order to use Go Portscan!")
    (confirm)
)


; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(closemedia #logo)
; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(showmedia '#iconpic' "images/icons.iff" "upper_left" "none" 0)

(set #iconchoice (askchoice
                    (prompt "Please choose the icon set you would like to use\n(If you have any important tooltypes set, then do not install these icons)")
                    (choices "GlowIcons (requires OS3.5+)" "NewIcons (by Luca Longone)" "NONE/Keep current icons")
                    (help "The installer is asking which set of icons you would like installed.\n\n'NewIcons' require the NewIcons patch installed, or OS3.5+ with NewIcons enabled.\n\n'GlowIcons' require OS3.5+\n\nThe standard 8 colour icons will work with any setup.\n\nNote: If you have any important tooltypes set on your current installation you should choose not to install the icons, otherwise they will be overwritten. You can still install the icons manually, later")
                    (default 0)
                    )
)

; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(closemedia #iconpic)

(if
    (= #iconchoice 0)
    (p_copy35icons)
)

(if (= #iconchoice 1)
    (p_copynicons)
)

; THIS LINE SHOULD BE COMMENTED OUT TO PRODUCE A PRE OS3.5 VERSION
;(openwbobject @default-dest)
